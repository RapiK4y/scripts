options:
    t: &7[&3&l!&7]
    c: <##DDDDBB>&m---------------------<##BBDDBB> &l&o스 무 고 개 <##DDDDBB>&m---------------------

command /출제자 [<OfflinePlayer>]:
    permission: op
    trigger:
        if arg-1 is set:
            if arg-1 is online:
                set {host} to arg-1
                set {theme.%arg-1%} to true
                teleport arg-1 to location(189.5, -21, 151.5, world "world", 90, 0)
                message "{@t} &e당신은 출제자입니다. 먼저 주제를 정해주세요. 명령어는 /주제 <단어> 입니다." to arg-1
                set {life} to 20
            else:
                message "{@t} &a%arg-1%&7님은 현재 오프라인입니다." to the player

command /주제 [<text>]:
    trigger:
        if {theme.%player%} is true:
            set {theme} to arg-1
            set {theme.%player%} to false

            if {themerecs::*} doesn't contain arg-1:
                add arg-1 to {themerecs::*}
            
            loop all players:
                if {gaming.%loop-player%} <= 1:
                    send "{@t} &f주제가 정해졌습니다. 주제: &a&l%arg-1%" to loop-player
            
            set {answer.%player%} to true
            message "{@t} &e정답을 입력해주세요. 주제와 관련된 것이여야만 합니다. 명령어는 /정답 <단어> 입니다." to the player
        else:
            message "{@t} 현재 출제자가 아닙니다." to the player

command /정답 [<text>]:
    trigger:
        if {answer.%player%} is true:
            set {answer} to arg-1
            set {answer.%player%} to false
            set {itime} to true

            loop all players:
                if {gaming.%loop-player%} is 1:
                    send "{@t} &f정답이 정해졌습니다." to loop-player
                    send "{@t} &f질서를 지키며 질문해주세요." to loop-player

            wait 2 seconds

            loop all players:
                if loop-player is not {host}:
                    if {gaming.%loop-player%} is 1:
                        teleport loop-players to location(176.5, -21, 151.5, world "world", -90, 0)
                        play sound "ENTITY_WITHER_SPAWN" to loop-players
                    
                    if {gaming.%loop-player%} is 1:
                        send "{@t} &f이제 질문이 가능합니다." to loop-player

            set {20game} to true
            set {itime} to false
            add {theme} to {thistory::*}
            add {host} to {hhistory::*}
            add "-" to {ahistory::*}
            add "-" to {chistory::*}
            add "-" to {lhistory::*}
        else:
            message "{@t} 현재 출제자가 아닙니다." to the player

command /정답입니다:
    permission: op
    trigger:
        if {itime} is not true:
            loop all players:
                if {gaming.%loop-player%} <= 1:
                    send "{@t} &a&l정답입니다!" to loop-player
                    send "{@t} &f정답: &a%{answer}%" to loop-player

                if {gaming.%loop-player%} is 1:
                    play sound "ENTITY_ENDER_DRAGON_DEATH" with volume 0.7 and pitch 2 to loop-player
                    send title "&f&l정답: &a&l%{answer}%" to loop-player for 5 seconds

            if {20game} is true:
                if {ask} is set:
                    set {chistory::%amount of {chistory::*}%} to "&6%{ask}%"
                else:
                    if {prev} is set:
                        set {chistory::%amount of {chistory::*}%} to "&6%{prev}%"
                    else:
                        set {chistory::%amount of {chistory::*}%} to "&7없음"
                if {life} >= 20:
                    set {lhistory::%amount of {lhistory::*}%} to "&f질문 &d0&f개 사용"
                else if {life} is between 15 and 19:
                    set {lhistory::%amount of {lhistory::*}%} to "&f질문 &b%20 - {life}%&f개 사용"
                else if {life} is between 10 and 14:
                    set {lhistory::%amount of {lhistory::*}%} to "&f질문 &a%20 - {life}%&f개 사용"
                else if {life} is between 5 and 9:
                    set {lhistory::%amount of {lhistory::*}%} to "&f질문 &e%20 - {life}%&f개 사용"
                else if {life} is between 1 and 4:
                    set {lhistory::%amount of {lhistory::*}%} to "&f질문 &c%20 - {life}%&f개 사용"
                set {ahistory::%amount of {ahistory::*}%} to {answer}
            set {20game} to false
            execute console command "초기화"
            
command /정답확인:
    permission: op
    trigger:
        message "{@t} &a&l정답&f: %{answer}%" to the player

command /주제확인:
    permission: op
    trigger:
        loop all players:
            if {gaming.%loop-player%} is 1:
                send "{@t} 현재 주제는 &d%{theme}%&7 입니다." to loop-player

command /추첨:
    permission: op
    trigger:
        if {host} is not set:
            set {_n} to 0
            loop all players:
                if {gaming.%loop-player%} is 1:
                    add 1 to {_n}
            
            if {_n} <= 0:
                send "{@t} 현재 스무고개에 참여한 인원이 없습니다!" to player
                stop

            Randomplayer()

            teleport {host} to location(189.5, -19, 151.5, world "world", 90, 0)

            set {theme.%{host}%} to true
            loop all players:
                if {gaming.%loop-player%} is 1:
                    send "{@t} 출제자를 무작위로 추첨합니다..." to loop-player

                    if {ipchat} is true:
                        send "{@t} 출제자는 &a%{ip2::%{host}%}%&7 입니다." to loop-player
                    else:         
                        send "{@t} 출제자는 &a%{host}%&7 입니다." to loop-player

            message "{@t} &e당신은 출제자입니다. 먼저 주제를 정해주세요. 명령어는 /주제 <단어> 입니다." to {host}
            set {life} to 20
        else:
            message "{@t} 출제자가 이미 존재합니다." to the player

function Randomplayer():
    set {_n} to 1
    while {_n} is 1:
        set {host} to random player out of all players
        if {gaming.%{host}%} is 1:
            set {_n} to 0

command /정답내역 [<integer>]:  
    trigger:
        if arg-1 is set:
            if arg-1 > 0:
                if {ahistory::%(10 * arg) - 9%} is set:
                    message "<##FFFFDD>&m------------------<##FFDDFF> &l스무고개 &f| <##FFDDFF>&l정답 내역 <##FFFFDD>&m------------------" to the player
                    message centered "        &f출제자   &7|   &f주제   &7|   &f정답   &7|   &f정답자   &7|   &f사용한 질문 개수" to the player
                    message " " to the player    
                    loop 10 times:
                        if {ahistory::%(10 * arg) - 10 + loop-num%} is set:
                            message "<##FFDDFF>##%(10 * arg) - 10 + loop-num%. <##FFDDDD>%{hhistory::%((amount of {hhistory::*}) - (10 * ((arg) - 1)) - ((loop-num) - 1))%}% &7| &f%{thistory::%((amount of {thistory::*}) - (10 * ((arg) - 1)) - ((loop-num) - 1))%}% &7| &f%{ahistory::%((amount of {ahistory::*}) - (10 * ((arg) - 1)) - ((loop-num) - 1))%}% &7| <##DDFFFF>%{chistory::%((amount of {chistory::*}) - (10 * ((arg) - 1)) - ((loop-num) - 1))%}% &7| %{lhistory::%((amount of {lhistory::*}) - (10 * ((arg) - 1)) - ((loop-num) - 1))%}%" to the player
                    if arg is 1:
                        message "&bPage &f%arg%&f/%ceil(amount of {ahistory::*} / 10)% <ttp:&fPage 2><cmd:/정답내역 2>▶" to the player
                    else if arg is ceil(amount of {ahistory::*} / 10):
                        message formatted "<ttp:&fPage %arg - 1%><cmd:/정답내역 %arg - 1%>◀<reset> &bPage &f%arg%&f/%ceil(amount of {ahistory::*} / 10)%" to the player
                    else:
                        message formatted "<ttp:&fPage %arg - 1%><cmd:/정답내역 %arg - 1%>◀<reset> &bPage &f%arg%&f/%ceil(amount of {ahistory::*} / 10)% <ttp:&fPage %arg + 1%><cmd:/정답내역 %arg + 1%>▶" to the player
                else:
                    message "{@t} 해당 페이지는 존재하지 않습니다." to the player
            else:
                message "{@t} 사용법: /정답내역 <페이지>" to the player
        else:
            message "{@t} 사용법: /정답내역 <페이지>" to the player

command /초기화:
    permission: op
    trigger:
        if {itime} is not true:
            if {20game} is true:
                execute console command "정답입니다"
            else:
                clear {ask}
                clear {prev}
                clear {theme}
                clear {answer}
                clear {life}
                clear {host}
                clear {itime}
                set {20game} to false

                loop all players:
                    clear {theme.%loop-player%}
                    clear {answer.%loop-player%}

                    if {gaming.%loop-player%} is 1:
                        send "{@t} &f초기화가 완료되었습니다." to loop-player

                    if {gaming.%loop-player%} is 1:
                        teleport loop-player to location(176.5, -21, 151.5, world "world", -90, 0)
                        play sound "ENTITY_WITHER_AMBIENT" to loop-player
        else:
            message "{@t} 초기화할 수 없습니다." to the player
           
command /lifedown:
    permission: op
    trigger:
        subtract 1 from {life}
        
        loop all players:
            if {gaming.%loop-player%} is 1:
                send "{@t} &6질문&f을 &c소모&f하였습니다. 남은 질문: &c%{life}%개" to loop-player
                loop 20 times:
                    play sound "BLOCK_GLASS_BREAK" with volume 0.7 to loop-player

        if {life} is 0:
            loop all players:
                if {gaming.%loop-player%} <= 1:
                    send "{@t} &c&l정답을 맞히지 못했습니다." to loop-player
                    send "{@t} &f정답: &a%{answer}%" to loop-player

            if {20game} is true:
                set {chistory::%amount of {chistory::*}%} to "&7없음"
                set {lhistory::%amount of {lhistory::*}%} to "&f질문 &420&f개 사용"
                set {ahistory::%amount of {ahistory::*}%} to {answer}
                set {20game} to false

            execute console command "초기화"
            
command /lifeup:
    permission: op
    trigger:
        add 1 to {life}

        loop all players:
            if {gaming.%loop-player%} is 1:
                send "{@t} &6질문 기회&f가 1회 증가했습니다. 남은 질문: &a%{life}%개" to loop-player

            if {gaming.%loop-player%} is 1:
                play sound "ENTITY_PLAYER_LEVELUP" with volume 0.7 to loop-player

command /life [<number>]:
    permission: op
    trigger:
        if arg-1 is set:
            if arg-1 >= 1:
                set {life} to arg-1
                broadcast "{@t} &f남은 &6질문&f이 &a%arg-1%&f개로 설정되었습니다."
            else:
                message "{@t} 1 자연수만 입력 가능합니다." to the player
        else:
            message "{@t} 사용법: /life <개수>" to the player

command /askclear:
    permission: op
    trigger:
        clear {ask}

        loop all players:
            if {gaming.%loop-player%} is 1:
                if "%region at loop-player%" contains "ask in world world":
                    teleport loop-player to location(176.5, -21, 151.5, world "world", -90, 0)
                play sound "ENTITY_EXPERIENCE_ORB_PICKUP" with volume 1 and pitch 1.5 to loop-player
            
        loop all players:
            if {gaming.%loop-player%} is 1:
                send "{@t} &f이제 질문이 가능합니다." to loop-player

on step on lime concrete:
    if {20game} is true:
        if {ask} is not set:
            set {ask} to player
            set {prev} to player
            if {ipchat} is true:
                loop all players:
                    if {gaming.%loop-player%} is 1:
                        send "{@t} &a%{ip2::%player%}%&f이(가) 질문대에 올라섰다." to loop-player
            else:
                loop all players:
                    if {gaming.%loop-player%} is 1:
                        send "{@t} &a%player%&f이(가) 질문대에 올라섰다." to loop-player

            play sound "ENTITY_PLAYER_LEVELUP" with volume 3 and pitch 2 at the player

on chat:
    if player is {ask}:
        set message to "&a%message%"
    if player is {host}:
        set message to "&6%message%"

on death:
    if {redfish::*} contains player:
        set gamemode of victim to spectator
        remove player from {redfish::*}


on join:
    give player stick of knockback 3 and smite 10
    give player glowing feather named "&f콩콩이"
    set {c.%uuid of player%} to true



on rightclick with feather:
	if {c.%uuid of player%} is not false:
		push the player forward with speed 1
		push the player upward with speed 1
		set {c.%uuid of player%} to false
		wait 3 seconds
		set {c.%uuid of player%} to true

command /스무고개현황:
    trigger:
        send "{@t} 총 %{appliedplayer}%명이 입장했습니다."
        if {gaming.%player%} is 1:
            send "{@t} 현재 입장 상태입니다."
        else:
            send "{@t} 현재 입장 상태가 아닙니다."


command /스무고개조정 <integer>:
    permission: op
    trigger:
        set {appliedplayer} to arg-1